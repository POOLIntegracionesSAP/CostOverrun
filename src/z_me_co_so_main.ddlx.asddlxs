@Metadata.layer: #CORE
@EndUserText.label: 'Cost Overrun for SO'
// Main Metadata Extension of the Cost Overrun application for Sales Order

// Order results by sales order, ascending direction
@UI.presentationVariant: [{ 
    sortOrder: [{ 
        by: 'salesorder',
        direction: #ASC
     }]
 }]
// Navigation page (by SO Item) defined with the sales order value as title and the SO Item value as description
@UI.headerInfo:
{ 
    title:
    {
        type: #STANDARD, value: 'salesorder'
    },
    description:
    { 
        value: 'salesorderitem'
    }
}

annotate view ZCDS_CO_SO_MAIN
    with
{
// Navigation page (UI facet) contains 4 details (for each SO Item): Detail Info of Sales Order, Summary of the Cost Overrun calculation, Journal Entries and Previous Postings
// For each detail, is defined the type (as List Report), the position in the screen and the association that provides the detailed info
    @UI.facet: [
        {
            id: 'SalesOrderInfo',
            purpose: #STANDARD,
            type: #LINEITEM_REFERENCE,
            label: 'Sales Order Detail',
            position: 10,
            targetElement: '_DetailSO'
        },
        {
            id: 'CalculationSummary',
            purpose: #STANDARD,
            type: #LINEITEM_REFERENCE,
            label: 'Calculation Summary',
            position: 20,
            targetElement: '_Calculation'
        },        
        {
            id: 'JournalEntries',
            purpose: #STANDARD,
            type: #LINEITEM_REFERENCE,
            label: 'Journal Entries',
            position: 30,
            targetElement: '_JournalEntries'
        },
        {
            id: 'Postings',
            purpose: #STANDARD,
            type: #LINEITEM_REFERENCE,
            label: 'Previous Postings',
            position: 40,
            targetElement: '_Posting'
        }
    ]
// Sales Organization defined as a selectionable field
    @UI.lineItem: [ { position : 10 } ]
    @UI.selectionField: [ { position : 20 } ]
    salesorganization;
// Sales Order defined as a selectionable field
    @UI.lineItem: [ { position : 20 } ]
    @UI.selectionField: [ { position : 30 } ]
    salesorder;
    @UI.lineItem: [ { position : 30 } ]
    salesorderitem;
    @UI.lineItem: [ { position : 40 } ]
    forecast;
    @UI.lineItem: [ { position : 50 } ]
    previousposting;
    @UI.lineItem: [ { position : 60 } ]
    totalamount;
    @UI.lineItem: [ { position : 70 } ]
    posting;
}